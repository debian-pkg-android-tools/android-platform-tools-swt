apply plugin: 'maven'

task generatePom(overwrite: true) {
  outputs.files "${buildDir}/${project.name}.pom"
  assemble.dependsOn generatePom
  doFirst {
    pom {
      project {
        if (hasProperty('pomName')) name pomName
        if (hasProperty('pomDesc')) description pomDesc
        url 'http://tools.android.com'
        inceptionYear '2007'
        licenses {
          license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }
        scm {
          url 'https://android.googlesource.com/platform/tools/swt'
          connection 'git://android.googlesource.com/platform/tools/swt.git'
        }
        developers {
          developer {
            name 'The Android Open Source Project'
          }
        }
      }
      whenConfigured {
        setDependencies(getDependencies().toSorted { a, b ->
          if ((a.scope <=> b.scope) != 0) {
            a.scope <=> b.scope
          } else if ((a.groupId <=> b.groupId) != 0) {
            a.groupId <=> b.groupId
          } else if ((a.artifactId <=> b.artifactId) != 0) {
            a.artifactId <=> b.artifactId
          } else {
            a.version <=> b.version
          }
        })
      }
    }.writeTo("${buildDir}/${project.name}.pom")
  }
  clean.doLast { delete "${buildDir}/${project.name}.pom" }
}